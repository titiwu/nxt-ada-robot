# $Id$
# Based on arm-elf-gcc-base by: Sergej Pupykin <pupykin.s+arch@gmail.com>
# By Titiwu

pkgname=arm-none-eabi-gcc-base
pkgver=4.6.1
pkgrel=1
pkgdesc="The GNU Compiler Collection"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
depends=('arm-none-eabi-binutils' 'libmpc' 'libelf')
options=(!libtool !emptydirs zipman docs !strip)
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-core-${pkgver}.tar.bz2)
sha256sums=('0bbb8f754a31f29013f6e9ad4c755d92bb0f154a665c4b623e86ae7174d98e33')

build() {
  cd $srcdir/gcc-$pkgver

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  [ $NOEXTRACT -eq 1 ] || rm -rf build
  mkdir build
  cd build

  [ $NOEXTRACT -eq 1 ] || ../configure --prefix=/usr \
	--target=arm-none-eabi \
	--host=$CHOST \
	--build=$CHOST \
	--enable-multilib \
	--with-newlib \
	--disable-nls \
	--disable-shared \
	--disable-threads \
	--disable-lto \
	--enable-languages=c \
	--disable-libssp \
	--without-headers \
	--with-local-prefix=/usr/lib/arm-none-eabi \
	--with-as=/usr/bin/arm-none-eabi-as \
	--with-ld=/usr/bin/arm-none-eabi-ld \
	--with-sysroot=/usr/$CHOST/arm-none-eabi

  make all-gcc
}

package() {
  cd $srcdir/gcc-$pkgver/build

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  make DESTDIR=$pkgdir install-gcc

  rm -f $pkgdir/usr/share/man/man7/fsf-funding.7*
  rm -f $pkgdir/usr/share/man/man7/gfdl.7*
  rm -f $pkgdir/usr/share/man/man7/gpl.7*
  rm -rf $pkgdir/usr/share/info

  cp -r $pkgdir/usr/libexec/* $pkgdir/usr/lib/
  rm -rf $pkgdir/usr/libexec

  # strip it manually
  strip $pkgdir/usr/bin/* 2>/dev/null || true
  find $pkgdir/usr/lib -type f -exec arm-none-eabi-strip {} \; 2>/dev/null || true
}

