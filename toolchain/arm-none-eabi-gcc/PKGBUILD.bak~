# Maintainer: Matias De la Puente <mfpuente.ar@gmail.com>
# based on 'cross-arm-elf-gcc' by Andreas Messer <andi@surveycorner.de>
# Patch from https://github.com/Lucretia/tamp

pkgname=cross-arm-eabi-gcc
pkgver=4.6.1
pkgrel=1
pkgdesc="The GNU Compiler Collection - Cross compiler for ARM target (with Ada)"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
#an installed libc/newlib is needed for libstdc++ compile
depends=('cross-arm-eabi-binutils>=2.21' 'cloog' 'ppl' 'cross-arm-eabi-newlib>=1.18.0')
# cross-arm-eabi-gcc is an superset of cross-arm-eabi-gcc-base
conflicts=('cross-arm-eabi-gcc-base')
provides=("cross-arm-eabi-gcc-base=${pkgver}")
options=(!libtool !emptydirs !strip zipman docs)
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2 gcc-${pkgver}.diff)
#gcc 4.6.2
#sha256sums=('60b05463dfe18d40d68fb8a71b25b408a01f86cc6ceaf5e6b22238b6b0f450c2')
#gcc 4.6.1 with patch
sha256sums=('8eebf51c908151d1f1a3756c8899c5e71572e8469a547ad72a1ef16a08a31b59'
            '8cb6da7f2e1e4abdc69479268139850203de54b77a77343ad917febcae74c3b1')

build() {

  cd ${srcdir}/gcc-$pkgver

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  patch -p1 -i ${srcdir}/gcc-${pkgver}.diff

  rm -rf build
  mkdir build
  cd build

  ../configure --prefix=/usr \
               --target=arm-eabi \
               --enable-interwork \
               --disable-nls \
               --enable-languages=c,c++,ada \
               --enable-multilib \
               --enable-interwork \
               --enable-libada    \
               --with-local-prefix=/usr/lib/cross-arm \
               --with-as=/usr/bin/arm-eabi-as \
               --with-ld=/usr/bin/arm-eabi-ld \
               --with-newlib \
               --with-float=soft \
               --host=$CHOST \
               --build=$CHOST 

  make || return 1
}

package() {
  cd ${srcdir}/gcc-$pkgver/build
  
  make install DESTDIR=${pkgdir} || return 1

  rm -f $pkgdir/usr/share/man/man7/fsf-funding.7*
  rm -f $pkgdir/usr/share/man/man7/gfdl.7*
  rm -f $pkgdir/usr/share/man/man7/gpl.7*
  rm -rf $pkgdir/usr/share/info
  rm -rf $pkgdir/usr/share/gcc-$pkgver

  cp -r  $pkgdir/usr/libexec/* $pkgdir/usr/lib/ && \
  rm -rf $pkgdir/usr/libexec 

}
