# Adapted from arm-elf-gcc-base: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Most of the patch is taken from The Ada Microkernel Project (TAMP)
# Contributor:  titiwu

pkgname=arm-none-eabi-gcc
pkgver=4.6.1
pkgrel=2
pkgdesc="The GNU Compiler Collection - Cross compiler for ARM target"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
#an installed libc/newlib is needed for libstdc++ compile
depends=('arm-none-eabi-binutils' 'cloog' 'ppl' 'arm-none-eabi-newlib')
# arm-none-eabi-gcc is an superset of arm-none-eabi-gcc-base
conflicts=('arm-none-eabi-gcc-base')
provides=("arm-none-eabi-gcc-base=${pkgver}")
replaces=('cross-arm-none-eabi-gcc')
options=(!libtool !emptydirs !strip zipman docs)
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2 gcc-${pkgver}-arm-none-eabi-ada.patch)
sha256sums=('8eebf51c908151d1f1a3756c8899c5e71572e8469a547ad72a1ef16a08a31b59'
            '184fff04bfcfa91a41dfe22e559e78dec0bd2aa3801ecd143f5b153035e8b611')

build() {
  cd ${srcdir}/gcc-$pkgver

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  echo "  >> Applying Patches to GNAT/GCC Cross..."

  patch -p1 -i ${srcdir}/gcc-${pkgver}-arm-none-eabi-ada.patch


  [ $NOEXTRACT -eq 1 ] || rm -rf build
  mkdir build
  cd build

  echo "  >> Begin Build ..."
  [ $NOEXTRACT -eq 1 ] || ../configure \
        --prefix=/usr \
	--target=arm-none-eabi \
	--host=$CHOST \
	--build=$CHOST \
  	--disable-shared \
	--disable-threads \
	--disable-lto \
	--disable-libssp \
        --disable-nls \
        --enable-languages=c,c++,ada \
        --enable-multilib \
        --enable-interwork \
	--with-local-prefix=/usr/lib/arm-none-eabi \
	--with-as=/usr/bin/arm-none-eabi-as \
        --with-ld=/usr/bin/arm-none-eabi-ld \
        --with-sysroot=/usr/$CHOST/arm-none-eabi\
	--with-newlib 

  make || return 1
}

package() {
  cd ${srcdir}/gcc-$pkgver/build

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  make DESTDIR=${pkgdir} install

  rm -f $pkgdir/usr/share/man/man7/fsf-funding.7*
  rm -f $pkgdir/usr/share/man/man7/gfdl.7*
  rm -f $pkgdir/usr/share/man/man7/gpl.7*
  rm -rf $pkgdir/usr/share/info
  rm -rf $pkgdir/usr/share/gcc-$pkgver
  rm -f  $pkgdir/usr/lib/libiberty.a

  cp -r  $pkgdir/usr/libexec/* $pkgdir/usr/lib/ && \
  rm -rf $pkgdir/usr/libexec 
}

