# Maintainer: Aliaksiej ArtamonaÅ­ <aliaksiej.artamonau@gmail.com>
# Patches from the fedora package as well as from Roman Shaposhnik (lowlevel-patch)
# Contributor: Titiwu
pkgname=libnxt
pkgver=0.3
pkgrel=4
pkgdesc='An utility library for talking to the LEGO Mindstorms NXT intelligent brick'

arch=('i686' 'x86_64')
url='http://code.google.com/p/libnxt/'
license=('GPL2')
depends=('libusb-compat>=0.1' 'libusb-compat<0.2')
makedepends=('scons')

source=(http://libnxt.googlecode.com/files/${pkgname}-${pkgver}.tar.gz
	libnxt-hashlib.patch
	libnxt-samba-endian-independent.patch
	libnxt-sconstruct.patch
	libnxt-lowlevel.patch
	libnxt-NXT-REFLASH-HOWTO)

md5sums=('05be5f5b8822f10e4fc4e1dea7d5e770'
         'ac4efce982b4c02d2b2a3f3eb333a488'
         '5c464f671d63190973e1312f5ae3659a'
         '08044533bca61db69e58aeb4f7f86aca'
         '2d916229eb55f567da85c61a97609632'
         '82091b6781fa3b8d6ea7b0ae25621971')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}/

  # patch source
  patch -p1 -i ${srcdir}/libnxt-hashlib.patch
  patch -p1 -i ${srcdir}/libnxt-samba-endian-independent.patch
  patch -p1 -i ${srcdir}/libnxt-sconstruct.patch
  patch -p1 -i ${srcdir}/libnxt-lowlevel.patch

   _soname=${pkgname}.so.0 _shname=${pkgname}.so.${pkgver} \
        scons -C ${srcdir}/${pkgname}-${pkgver} || return 1

}

package() {

    install -d -m755 ${pkgdir}/usr/include/libnxt || return 1
    install -m644 -t ${pkgdir}/usr/include/libnxt \
                     ${srcdir}/${pkgname}-${pkgver}/*.h || return 1

    install -d -m755 ${pkgdir}/usr/bin || return 1
    install -m755 -t ${pkgdir}/usr/bin \
                     ${srcdir}/${pkgname}-${pkgver}/{fwexec,fwflash} || return 1

    install -d -m755 ${pkgdir}/usr/lib || return 1
    install -m644 -t ${pkgdir}/usr/lib \
                     ${srcdir}/${pkgname}-${pkgver}/libnxt.a || return 1

    install -m755 -t ${pkgdir}/usr/lib \
                     ${srcdir}/${pkgname}-${pkgver}/${pkgname}.so.${pkgver} || \
        return 1
    ldconfig -N ${pkgdir}/usr/lib || return 1
    ln -sf ${pkgname}.so.0 ${pkgdir}/usr/lib/${pkgname}.so || return 1

}
